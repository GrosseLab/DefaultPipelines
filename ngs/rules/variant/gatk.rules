#############################################################################
#          Rules of the Genome Analysis Toolkit pipeline.                   #
#                                                                           #
# Date: 17.02.2015                                                          #
#                                                                           #
#############################################################################

# Local realignment around indels

rule realign_info:
   """Determines critical regions for realignment."""
    input: bam = "results/mapping/{reference}/{kind}/{prefix}.bam",
           bai = "results/mapping/{reference}/{kind}/{prefix}.bam.bai",
           dbsnp = config["ref_dir"] + config["known_variants"]["dbsnp"],
           ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           idx = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference] + ".fai",
           seqdic = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference][:-3] + ".dict"
    output: "results/mapping/{reference}/{kind}/{prefix}_realign_intervals.list"
    threads: 10
    shell: "java -Xmx50g -jar /usr/local/bin/GenomeAnalysisTK-3.2-2/GenomeAnalysisTK.jar "
           "-T RealignerTargetCreator "
           "-nt {threads} "
           "-known {input.dbsnp} "
           "-R {input.ref} "
           "-I {input.bam} "
           "-o {output}"

rule realign_reads:
    """Realigns reads on critical regions."""
    input: bam = "results/mapping/{reference}/{kind}/{prefix}.bam",
           bai = "results/mapping/{reference}/{kind}/{prefix}.bam.bai",
           ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           interval = "results/mapping/{reference}/{kind}/{prefix}_realign_intervals.list"
    output: "results/mapping/{reference}/{kind}/{prefix}_realign.bam"
    shell: "java -Xmx50g -jar /usr/local/bin/GenomeAnalysisTK-3.2-2/GenomeAnalysisTK.jar "
           "-T IndelRealigner "
           "--disable_bam_indexing "
           "-R {input.ref} "
           "-I {input.bam} "
           "-targetIntervals {input.interval} "
           "-o {output}"

# Quality score recalibration

rule recalibrate_info:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           dbsnp = config["ref_dir"] + config["known_variants"]["dbsnp"],
           bam = "results/mapping/{reference}/{kind}/{prefix}.bam",
           bai = "results/mapping/{reference}/{kind}/{prefix}.bam.bai"
    output: "results/mapping/{reference}/{kind}/{prefix}_recalibrate.grp"
    log:
        "results/mapping/log/{reference}/{kind}/{prefix}_recalibrate_info.log"
    threads: 8
    shell: "java -Xmx50g -jar /usr/local/bin/GenomeAnalysisTK-3.2-2/GenomeAnalysisTK.jar "
           "-T BaseRecalibrator -R {input.ref} "
           "-nct {threads} "
           "-I {input.bam} "
           "-knownSites {input.dbsnp} "
           "-o {output} >& {log}"
    
rule recalibrate_bam:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           bam = "results/mapping/{reference}/{kind}/{prefix}.bam",
           grp = "results/mapping/{reference}/{kind}/{prefix}_recalibrate.grp"
    output: "results/mapping/{reference}/{kind}/{prefix}_recalibrated.bam"
    log: "results/mapping/log/{reference}/{kind}/{prefix}_recalibrate.log"
    threads: 8
    shell: "java -Xmx50g -jar /usr/local/bin/GenomeAnalysisTK-3.2-2/GenomeAnalysisTK.jar "
           "-T PrintReads -R {input.ref} "
           "-nct {threads} "
           "--disable_bam_indexing "
           "-I {input.bam} -BQSR {input.grp} "
           "-o {output} >& {log}"

# Haplotype caller

rule haplotype_caller:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           bam = "results/mapping/{reference}/{kind}/{prefix}.bam",
           bai = "results/mapping/{reference}/{kind}/{prefix}.bam.bai"
    output: "results/variants/{reference}/{kind}/{prefix}.vcf"
    shell: "java -jar /usr/local/bin/GenomeAnalysisTK-3.2-2/GenomeAnalysisTK.jar "
           "-T HaplotypeCaller "
           "-R {input.ref} "
           "-I {input.bam} "
           "-o {output}"
