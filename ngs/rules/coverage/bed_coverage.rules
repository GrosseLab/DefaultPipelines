#############################################################################
#          Rules to generate coverage files given bed features.             #
#                                                                           #
# Date: 02.01.2015                                                          #
#                                                                           #
#############################################################################

rule target_coverage:
    """Compute per base coverage of sequenced regions."""
    input:
        ref = config["ref_dir"] + "/{platform}/{ref}/kits/{manifest}_ensemble_{bef}_{aft}.bed",
        bams = lambda wildcards: expand("results/mapping/" + wildcards.mapper + "/" + wildcards.ref + "/" + wildcards.kind + "/{unit}_coord.bam", unit = config["samples"][wildcards.sample])          
    output: "results/coverage/{mapper}/{ref}/{kind}/{sample}_{platform}_{manifest}_{bef}_{aft}.cov"
    threads: 15
    run:
        if len(input.bams) == 1:
            command = "cat"
        else:
            command = "samtools merge -"
        shell("{command} {input.bams} | bedtools coverage -split -d -b stdin -a {input.ref} > {output}")

rule target_bad_coverage:
    """Only output Regions under a given covergae"""
    input: "results/coverage/{mapper}/{ref}/{kind}/{sample}_{platform}_{manifest}_{bef}_{aft}.cov"
    output: "results/coverage/{mapper}/{ref}/{kind}/{sample}_{platform}_{manifest}_{bef}_{aft}.lower{mincov}"
    shell: "cat {input} | awk '$6 < {wildcards.mincov}' > {output}"

rule target_coverage_hist:
    """Compute per base coverage of sequenced regions."""
    input:
        bams = lambda wildcards: expand("results/mapping/" + wildcards.mapper + "/" + wildcards.ref + "/" + wildcards.kind + "/{unit}_coord.bam", unit = config["samples"][wildcards.sample]),
        manifest = config["ref_dir"] + "/{platform}/{ref}/kits/{manifest}_ensemble_{bef}_{aft}.bed" 
    output: "results/coverage/{mapper}/{ref}/{kind}/{sample}_{platform}_{manifest}_{bef}_{aft}.hist"
    threads: 15
    run:
        if len(input.bams) == 1:
            command = "cat"
        else:
            command = "samtools merge -"
        shell("{command} {input.bams} | bedtools coverage -hist -b stdin -a {input.manifest} | grep ^all > {output}")
