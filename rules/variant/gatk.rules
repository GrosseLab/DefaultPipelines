#############################################################################
#          Rules of the Genome Analysis Toolkit pipeline.                   #
#                                                                           #
# Date: 17.02.2015                                                          #
#                                                                           #
#############################################################################

# Local realignment around indels
# see http://gatkforums.broadinstitute.org/discussion/1247/what-should-i-use-as-known-variantssites-for-running-tool-x and ftp://ftp.broadinstitute.org//bundle/2.8/hg19 for used SNP/Indel-DBs
from math import floor

# GenomeAnalysisTK.jar is available via gatk

ruleorder: gatk_genotyping_all > gatk_genotyping

rule realign_info:
   """Determines critical regions for realignment."""
    input: bam = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.bam",
           bai = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.bam.bai",
           snpdb_1 = config["ref_dir"] + "/human/{reference}/snp/Mills_and_1000G_gold_standard.indels.{reference}.sites.vcf",
           snpdb_2 = config["ref_dir"] + "/human/{reference}/snp/1000G_phase1.indels.{reference}.sites.vcf",
           ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           idx = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference] + ".fai",
           seqdic = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference][:-3] + ".dict"
    output: temp("results/mapping/{mapper}/{reference}/{kind}/{prefix}.realign.intervals.list")
    threads: 8
    shell: "gatk -T RealignerTargetCreator "
           "-nt {threads} "
           "-known {input.snpdb_1} "
           "-known {input.snpdb_2} "
           "-R {input.ref} "
           "-I {input.bam} "
           "-o {output}"

rule realign_reads:
    """Realigns reads on critical regions."""
    input: bam = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.bam",
           bai = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.bam.bai",
           ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           snpdb_1 = config["ref_dir"] + "/human/{reference}/snp/Mills_and_1000G_gold_standard.indels.{reference}.sites.vcf",
           snpdb_2 = config["ref_dir"] + "/human/{reference}/snp/1000G_phase1.indels.{reference}.sites.vcf",
           interval = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.realign.intervals.list"
    output: "results/mapping/{mapper}/{reference}/{kind}/{prefix}.realign.bam"
    shell: "gatk -T IndelRealigner "
           "--disable_bam_indexing "
           "-R {input.ref} "
           "-I {input.bam} "
           "-known {input.snpdb_1} "
           "-known {input.snpdb_2} "
           "-targetIntervals {input.interval} "
           "-o {output}"

# Quality score recalibration

rule recalibrate_info:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           snpdb_1 = config["ref_dir"] + "/human/{reference}/snp/Mills_and_1000G_gold_standard.indels.{reference}.sites.vcf",
           snpdb_2 = config["ref_dir"] + "/human/{reference}/snp/1000G_phase1.indels.{reference}.sites.vcf",
           snpdb_3 = config["ref_dir"] + "/human/{reference}/snp/dbsnp_138.{reference}.vcf",
           bam = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.bam",
           bai = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.bam.bai"
    output: temp("results/mapping/{mapper}/{reference}/{kind}/{prefix}.recalibrate.grp")
    threads: 8
    shell: "gatk -T BaseRecalibrator -R {input.ref} "
           "-nct {threads} "
           "-I {input.bam} "
           "-knownSites {input.snpdb_1} "
           "-knownSites {input.snpdb_2} "
           "-knownSites {input.snpdb_3} "
           "-o {output}"
    
rule recalibrate_bam:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.reference],
           bam = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.bam",
           grp = "results/mapping/{mapper}/{reference}/{kind}/{prefix}.recalibrate.grp"
    output: "results/mapping/{mapper}/{reference}/{kind}/{prefix}.recalibrated.bam"
    threads: 8
    shell: "gatk -T PrintReads -R {input.ref} "
           "-nct {threads} "
           "--disable_bam_indexing "
           "-I {input.bam} -BQSR {input.grp} "
           "-o {output}"

# Haplotype caller

rule haplotype_caller:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.ref],
           bams = lambda wildcards: expand("results/mapping/bwa/" + wildcards.ref + "/" + wildcards.kind + "/{unit}_coord_dedup.realign.recalibrated.bam", unit = config["samples"][wildcards.patient]),
           bais = lambda wildcards: expand("results/mapping/bwa/" + wildcards.ref + "/" + wildcards.kind + "/{unit}_coord_dedup.realign.recalibrated.bam.bai", unit = config["samples"][wildcards.patient]),
           snpdb = config["ref_dir"] + "/human/{ref}/snp/dbsnp_138.{ref}.vcf"
    output: "results/variants/gatk/{ref}/{kind}/{patient}.g.vcf"
    threads: 8
    run: 
      bams = " -I ".join(input.bams)
      nct = floor(threads / len(input.bams))
      shell("gatk -T HaplotypeCaller "
           "-R {input.ref} "
           "-I {bams} "
           "--emitRefConfidence GVCF "
           "--variant_index_type LINEAR "
           "--variant_index_parameter 128000 "
           "--dbsnp {input.snpdb} "
           "--num_cpu_threads_per_data_thread {nct} "
           "-o {output}")

rule gatk_genotyping_all:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.ref],
           gvcfs = lambda wildcards: expand("results/variants/gatk/" + wildcards.ref + "/" + wildcards.kind + "/{patient}.g.vcf", patient=config["samples"].keys())
    output: "results/variants/gatk/{ref}/{kind}/all.vcf"
    threads: 15
    run:
      gvcfs = " --variant ".join(input.gvcfs)
      shell("gatk -T GenotypeGVCFs "
            "--variant {gvcfs} "
            "-nt {threads} "
            "-R {input.ref} "
            "-o {output}")

rule gatk_genotyping:
    input: ref = lambda wildcards: config["ref_dir"] + config["references"][wildcards.ref],
           gvcf = "results/variants/gatk/{ref}/{kind}/{patient}.g.vcf"
    output: "results/variants/gatk/{ref}/{kind}/{patient}.vcf"
    threads: 15
    run:
      shell("gatk -T GenotypeGVCFs "
            "--variant {input.gvcf} "
            "-nt {threads} "
            "-R {input.ref} "
            "-o {output}")

rule collect_genotypes_for_SNPs:
    input: bams = lambda wildcards: expand("results/mapping/illumina/" + wildcards.ref + "/" + wildcards.kind + "/{sample}_coord.bam", sample = config["units"].keys()),
           index = lambda wildcards: expand("results/mapping/illumina/" + wildcards.ref + "/" + wildcards.kind + "/{sample}_coord.bam.bai", sample = config["units"].keys()),
           snp_list = config["ref_dir"] + "/human/hg19/snp/rs_snps_cf-deepseq.txt"
    output: "results/variants/gatk/{ref}/{kind}/{any}_rs_SNPs.tsv"
    shell: "echo {input.bams} | genotype_collector.pl -o {output} -s {input.snp_list}"
